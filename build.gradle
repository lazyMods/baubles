buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'


group = 'com.lazy.baubles'
version = "${project.mod_version}-${project.mc_version}"

java {
    archivesBaseName = 'baubles'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: "${project.channel}", version: "${project.mappings}"
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                baubles { source sourceSets.main }
                baubles_test { source sourceSets.test }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                baubles { source sourceSets.main }
                baubles_test { source sourceSets.test }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'baubles', '--all', '--output', file('src/generated/resources/')
            mods {
                baubles { source sourceSets.main }
                baubles_test { source sourceSets.test }
            }
        }
    }
}

repositories {
    maven {
        url 'https://pkgs.dev.azure.com/lazyio/maven/_packaging/lazy/maven/v1'
        name 'lazy'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
}

jar {
    manifest {
        attributes([
            "Specification-Title": "baubles",
            "Specification-Vendor": "baublessareus",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"baublessareus",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

task apiJar(type: Jar) {
    // Sources included because of MinecraftForge/ForgeGradle#369
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include 'lazy/baubles/api/**'
    classifier("api")
}

artifacts {
    archives(apiJar)
}

publishing {
    tasks.publish.dependsOn build

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def user = properties.getProperty('user')
    def pass = properties.getProperty('pass')

    publications {
        mavenJava(MavenPublication) {
            groupId = 'lazy'
            artifactId = 'baubles'
            version = "${mod_version}"
            artifact apiJar
            from components.java
            pom.withXml {
                def node = asNode()
                if(node.dependencies.size() > 0)
                    node.remove(node.dependencies) // Remove deps, as they are all mappings-dependent and/or forge
            }
        }
    }

    repositories {
        maven {
            url = 'https://pkgs.dev.azure.com/lazyio/maven/_packaging/lazy/maven/v1'
            name 'lazy'
            credentials {
                username = user
                password = pass
            }
        }
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}